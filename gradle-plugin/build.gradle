import org.gradle.api.internal.artifacts.dependencies.DefaultProjectDependency

// ============================================================================
// (C) Copyright Schalk W. Cronje 2013
//
// This software is licensed under the Apache License 2.0
// See http://www.apache.org/licenses/LICENSE-2.0 for license details
//
// Unless required by applicable law or agreed to in writing, software distributed under the License is
// distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and limitations under the License.
//
// ============================================================================

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
        classpath "org.ysb33r.gradle:gradletest:0.5.4"
    }
}

apply plugin : 'com.gradle.plugin-publish'
apply plugin : 'org.ysb33r.gradletest'

group = 'org.ysb33r.gradle'
archivesBaseName = 'vfs-gradle-plugin'

ext {
    gradleID = 'org.ysb33r.vfs'
    moduleName = 'vfs-gradle-plugin'
    bintrayDescription = 'This is a plugin for Gradle that utilises the Groovy VFS DSL'
    bintrayTags = ['gradle','groovy','vfs','groovy-vfs']
    bintrayAttributes = [ 'gradle-plugin' : "org.ysb33r.vfs:${group}:${archivesBaseName}" ]

    dslProject = dependencies.project( path:':groovy-vfs',configuration:'default')
}

configurations.all { exclude module: 'groovy-all' }

dependencies {
    compile dslProject
//    compile project( path:':dsl',configuration:'default')
    compile gradleApi()
    compile localGroovy()

    compile 'org.slf4j:jcl-over-slf4j:1.7.2'

    testCompile project(':test-servers')

//    gradleTest project( path:':dsl',configuration:'default')
    gradleTest dslProject
    gradleTest 'commons-httpclient:commons-httpclient:3.1'

}


test {
    systemProperties TESTFSREADROOT : projectDir
}

jar {
    manifest {
        attributes 'Implementation-Title': moduleName, 'Implementation-Version': version
    }
}

apply from : '../gradle/publish.gradle'


pluginBundle {
    description = bintrayDescription
    website     = 'http://ysb33r.github.io/groovy-vfs'
    vcsUrl      = 'https://github.com/ysb33r/groovy-vfs.git'

    tags = bintrayTags

    plugins {
        vfsPlugin {
            id = gradleID
            displayName = 'Gradle VFS plugin'
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = archivesBaseName
    }

}

publishPlugins {
    onlyIf { !version.endsWith("SNAPSHOT") }
}

gradleTest {
    versions '2.0' ,'2.2', '2.3', '2.5', '2.8', '2.10', '2.12', '2.13'

    onlyIf { !gradle.startParameter.isOffline() }
}


